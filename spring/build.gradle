
buildscript {
    ext.kotlin_version = '1.9.25'
    ext.jackson_version = '2.17.1'
    ext.boot_version = "2.7.18"
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "${kotlin_version}"
    id "org.jetbrains.kotlin.plugin.spring" version "${kotlin_version}"
    id "idea"
    id "java-library"
    id "maven-publish"
}

kotlin {
    jvmToolchain(17)
    compilerOptions {
        freeCompilerArgs.add("-Xjsr305=strict")
    }
}

repositories {
    maven { url 'https://repository.jboss.org/nexus/content/groups/public/' }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation('io.opencui:core') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    implementation "org.springframework.boot:spring-boot-starter-actuator:${boot_version}"
    implementation "org.springframework.boot:spring-boot-starter-webflux:${boot_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-compiler"
    implementation "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jackson_version}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jackson_version}"
    implementation "redis.clients:jedis:3.2.0"
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
}

jar.enabled = true

group = "io.opencui.dispatcher"
version = '1.0-SNAPSHOT'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        def repo_dir = "${layout.buildDirectory.get()}/repos/"
        maven {
            url = uri(version.endsWith('SNAPSHOT') ?
                    "${repo_dir}/snapshots" : "${repo_dir}/releases")
        }
    }
}
